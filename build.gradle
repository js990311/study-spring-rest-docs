plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.epages.restdocs-api-spec' version "0.18.2"
}

group = 'com.study'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// rest docs and open api
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'
}

openapi3 {
	server  = 'localhost:8080'
	title = "Anki"
	description = "Anki 작업"
	version="0.1.0"
	format = "yaml"
}


tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}


asciidoctor{
	configurations 'asciidoctorExt'
	baseDirFollowsSourceDir()
	inputs.dir snippetsDir
	dependsOn test
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}

task createDocument(type: Copy){
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file('src/main/resources/static/docs')
}

bootJar{
	dependsOn createDocument
	from("${asciidoctor.outputDir}"){
		into 'static/docs'
	}
}


tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

tasks.register('copyOasToSwagger', Copy) {
	doFirst {
		delete file('src/main/resources/static/swagger-ui/openapi3.yaml')
	}
	from file('build/api-spec/openapi3.yaml')
	into file('src/main/resources/static/docs/.')
	dependsOn tasks.named('openapi3')
}
